( //////////////////////////////////////////////////////////////////////////////////////// )
( //                                                                                       )
( //    Copyright 2023 James Patrick Norris                                                )
( //                                                                                       )
( //    This file is part of Diaperglu 5.8.                                                )
( //                                                                                       )
( //    Diaperglu 5.8 is free software; you can redistribute it and/or modify              )
( //    it under the terms of the GNU General Public License as published by               )
( //    the Free Software Foundation; either version 2 of the License, or                  )
( //    {at your option} any later version.                                                )
( //                                                                                       )
( //    Diaperglu 5.8 is distributed in the hope that it will be useful,                   )
( //    but WITHOUT ANY WARRANTY; without even the implied warranty of                     )
( //    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the                      )
( //    GNU General Public License for more details.                                       )
( //                                                                                       )
( //    You should have received a copy of the GNU General Public License                  )
( //    along with Diaperglu 5.8; if not, write to the Free Software                       )
( //    Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA          )
( //                                                                                       )
( //////////////////////////////////////////////////////////////////////////////////////// )

( /////////////////////////////// )
( // James Patrick Norris      // )
( // www.rainbarrel.com        // )
( // March 27, 2023            // )
( // version 5.8               // )
( /////////////////////////////// )


// last changed March 29, 2022

// This makes a hello world .o file

// Mac OS X deprecated SYSCALL so you need to use imported functions
//  on the plus side, imported functions execute faster than syscalls

NEW-FLAT-OSYMBOL-BUF
( -R- oldcurrentcompilebufid )
( -EH- importelementid_importhlistid exportelementid_exporthlistid )
( // current compile buffer set to new buffer ) 

X86-WORDLIST >SEARCH-ORDER

DECIMAL

OSYMBOL omyhelloworldmessage
13 CODE-U8,  10 CODE-U8,                          // pushes crlf to the buffer
$" Hello World!" PCURRENTCOMPILEBUFFER @ $>BUF    // pushes the message to the buffer
13 CODE-U8,  10 CODE-U8,                          // appends another crlf
13 CODE-U8,  10 CODE-U8,                          // and another crlf

OSYMBOL myhelloworldstart 
   // align the stack regardless of where it is when this function is called
   HEX -10 N  RSP  AND,
   
   1 N  RDI  MOV,                                                  // stdout handle
   EH. omyhelloworldmessage [O]  RSI  LEA,                         // pmessageaddress
   EH. myhelloworldstart EH. omyhelloworldmessage - N  RDX  MOV,   // messagelength

IMP CALL,  OSYMBOL-IMPORT write

IMP CALL,  OSYMBOL-IMPORT exit

PCURRENTCOMPILEBUFFER @ 
EH 
1 EH[ND]
BUF>NEWEXPORTIMPORT.OBUF

DUP $" helloworld64.o" SAVEFILE$
FREEBUFFER

SEARCH-ORDER> DROP

FREE-FLAT-OSYMBOL-BUF
( // current compile buf is freed )
( //  and current compile buf is set to the oldcurrentcompilebbuf )
( oldcurrentcompilebufid -R- )
( importelementid_importhlistid exportelementid_exporthlistid -EH- )

TRUE ?END-EVALUATE


// ////////////////////////////////////////////////
//  After here is another way to do hello world. //
// ////////////////////////////////////////////////

// last changed April 12, 2020


// adding the assembler to the search order

X86-WORDLIST >SEARCH-ORDER


// make new empty compile buffer

HEX 10000 -1 NEWBUFFER CONSTANT MYCOMPILEBUFID
MYCOMPILEBUFID PCURRENTCOMPILEBUFFER !

 
// making an hlist to hold a symbol list and making it the current elementid hlistid

NEW-NGLULIST>EH

( noparentelementid_myhlistid -EH- )


 // adding a root element to the current EH hlist
 //  this element is not used other than as a parent to the import symbol list elements
$" myimportsymbollistparent" 0 >NEW$ EH-NEW-ELEMENT>EH

( noparentelementid_myhlistid importsparentelementid_myhlistid -EH- )

// it would be nice to be able to add symbol lists to the search order... 12/25/2019


// adding another root element to the current EH hlist
//  this element is not used other than as a parent to the export symbol list elements

$" myexportsymbollistparent" 0 >NEW$
1 EH[ND] // noparentelementid_myhlistid on data stack
DUP >R   // saving a copy of the myhlistid
NEW-ELEMENT
R> >EH

( noparentelementid_myhlistid importsparentelementid_myhlistid exportsparentelemendid_mylistid -EH- )


HEX
OHERE CONSTANT omyhelloworldmessage
  $" Hello World!" HEX 0A C>$ LENGTH$ CONSTANT myhelloworldmessagelength 
  MYCOMPILEBUFID $>BUF


HEX
OSYMBOL myhelloworldstart

  -10 N  RSP  AND,                                         // align the return stack
                                                           //  not worried about losing the return
                                                           //  value because this function does not
                                                           //  not do a return

  1 N RDI MOV,                                             // rdi = hstdout
  myhelloworldmessagelength N RDX MOV,                     // rdx = count
  omyhelloworldmessage [O] RSI LEA,                        // rsi = pstring 
  RIP 0 [R+N] CALL,                                        // this will call the import function
  OHERE 4 - >NEW$  $" write"  1 EH[ND]  NEW-ELEMENT  DROP  // this adds the import

  RIP 0 [R+N] CALL,                                        // this will call the import function
  OHERE 4 - >NEW$  $" exit"  1 EH[ND]  NEW-ELEMENT  DROP   // this adds the import
  
  // ld makes an import table for you with one import per unique symbol name, and will
  //   fill in the offset from your [RIP+N] instruction to the import table at link time
  // At this time, I do not know how to tell ld which import symbol comes from which library. 12/26/2019
  

// saving the compiled buffer in the mach-o .o format

MYCOMPILEBUFID
EH>                                              // export symbol list parent
EH>                                              // import symbol list parent
BUF>NEWMACH-O.OBUF

$" helloworld64.o" SAVEFILE$



